Use DFUnit.pkg
Use cArrayIteratorInteger.pkg

Object ArrayIteratorInteger_spec is a cTestFixture
    Procedure Setup
        Object oIterator is a cArrayIteratorInteger
        End_Object
        
        Integer[] iArray
        Integer iCount
        For iCount from 0 to 19
            Move iCount to iArray[iCount]
        Loop
        Send AttachArray to oIterator iArray
    End_Procedure
    Procedure TearDown
        Send Destroy of oIterator
    End_Procedure
    Object IteratorReset_IteratorMoveNext_CurrentArrayValue_Returns_0 is a cTest
        Procedure Test
            Send IteratorReset to oIterator
            Boolean bFoundFirstElement
            Get IteratorMoveNext of oIterator to bFoundFirstElement
            Send Assert bFoundFirstElement "First element of iterator not found"
            Send AssertIAreEqual 0 (CurrentArrayValue(oIterator(Self)))
        End_Procedure
    End_Object
    Object IteratorMoveNext_20_times_Returns_False is a cTest
        Procedure Test
            Send IteratorReset to oIterator
            Integer iCount
            Boolean bFound
            For iCount from 1 to 20
                Get IteratorMoveNext of oIterator to bFound
                Send Assert bFound ("Iterator should contain 20 elements, but found only" * (iCount - 1))
                If (not(bFound)) Procedure_Return
            Loop
            Get IteratorMoveNext of oIterator to bFound
            Send AssertFalse bFound "Iterator should not contain more than 20 elements"
        End_Procedure
    End_Object
End_Object